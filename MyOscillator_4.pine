//@version=4
study(title="My Oscillator", shorttitle="MOv4", overlay=false)

//Interior
n1 = input(9, "Channel Length")
n2 = input(26, "Average Length")
n3 = input(13, "Short length")
smo0 = input(false, "Smoothing")
upper_limit = input(80, "Upper Limit")
lower_limit = input(20, "Lower Limit")

//Engine
src1 = security(syminfo.tickerid, timeframe.period, hlc3)

ttsi(_src, _len0, _len1) =>
    pc = change(_src) / avg(_src, _src[1])
    ma0 = ema(pc, _len0)
    ma1 = ema(ma0, _len1)
    apc = abs(_src - _src[1])
    ma2 = ema(apc, _len0)
    ma3 = ema(ma2, _len1)
    ttsi = 100 * (ma1 / ma3)

tci(_src) => 
    ema((_src - ema(_src, n1)) / (0.025 * ema(abs(_src - ema(_src, n1)), n1)), n2) + 50

mf(_src) => 
    rsi(sum(volume * (change(_src) <= 0 ? 0 : _src), n3), sum(volume * (change(_src) >= 0 ? 0 : _src), n3))

willy(_src) => 
    60 * (_src - highest(_src, n2)) / (highest(_src, n2) - lowest(_src, n2)) + 80

csi(_src) => 
    avg(rsi(_src, n3), ttsi(_src, n1, n2) * 50 + 50)

godmode(_src) => 
    avg(tci(_src), csi(_src), mf(_src), willy(_src))

tradition(_src) => 
    avg(tci(_src), mf(_src), rsi(_src, n3))

wt1 = godmode(src1)
wt2 = ema(wt1, 6)
wt3 = linreg((wt1 - wt2) * 2 + 50, n3, 0)

//Chassis
col1 = wt3 > 50 ? color.green : color.red
col2 = wt3 > 50 and wt3[1] > wt3 ? color.white : (wt3 < 50 and wt3[1] < wt3 ? color.white : na)

//when smoothing
swt1 = sma(wt1, n1)
swt2 = sma(wt2, n1)
swt3 = sma(wt3, n1)

scol1 = swt3 > 50 ? color.green : color.red
scol2 = swt3[0] > swt3[1] ? color.green : color.red

extended1 = smo0 ? (swt2 < lower_limit+15 ? swt2 - 5 : swt2 > upper_limit-15 ? swt2 + 5 : na) : wt2 < lower_limit+10 ? wt2 - 5 : wt2 > upper_limit-10 ? wt2 + 5 : na
extended2 = smo0 ? (swt2 < lower_limit+10 ? swt2 - 5 : swt2 > upper_limit-10 ? swt2 + 5 : na) : wt2 < lower_limit+5  ? wt2 - 5 : wt2 > upper_limit-5  ? wt2 + 5 : na
extended3 = smo0 ? (swt2 < lower_limit+5  ? swt2 - 5 : swt2 > upper_limit-5  ? swt2 + 5 : na) : wt2 < lower_limit    ? wt2 - 5 : wt2 > upper_limit    ? wt2 + 5 : na
extended4 = smo0 ? (swt2 < lower_limit    ? swt2 - 5 : swt2 > upper_limit    ? swt2 + 5 : na) : wt2 < lower_limit-5  ? wt2 - 5 : wt2 > upper_limit+5  ? wt2 + 5 : na

plot(smo0 ? swt1 : wt1, color=color.red, linewidth=2, editable=false)
plot(smo0 ? swt2 : wt2, color=color.green, linewidth=2, editable=false)

//histogram
plot(smo0 ? swt3 : wt3, color=smo0 ? scol1 : col1, linewidth=1, style=plot.style_columns, histbase=50, transp=50, editable=false)
plot(smo0 ? swt3 : wt3, color=smo0 ? scol2 : col2, linewidth=1, style=plot.style_columns, histbase=50, transp=50, editable=false)

//caution signals
plot(extended1, color=color.aqua, style=plot.style_circles, linewidth=2, editable=false)
plot(extended2, color=color.yellow, style=plot.style_circles, linewidth=2, editable=false)
plot(extended3, color=color.orange, style=plot.style_circles, linewidth=2, editable=false)
plot(extended4, color=color.red, style=plot.style_circles, linewidth=2, editable=false)

plot(smo0 ? swt3[1] : wt3[1], color=smo0 ? scol2 : col2, linewidth=1, style=plot.style_line, editable=false, display=display.none)