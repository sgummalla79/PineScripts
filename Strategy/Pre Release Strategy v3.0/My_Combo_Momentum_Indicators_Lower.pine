//@version=4
study(title="My_Combo_Momentum_Indicators_Lower", shorttitle="MCMIL")

indicator_type = input(defval='Moxie Arrows', title='Select Indicator', options=['Squeeze Momentum', 'True Momentum Oscillator', 'Moxie Arrows'])
indicator_num = indicator_type == 'Squeeze Momentum' ? 1 : indicator_type == 'True Momentum Oscillator' ? 2 : indicator_type == 'Moxie Arrows' ? 3 : na

msqz_plot = indicator_num == 1
tmo_plot = indicator_num == 2
moxie_plot = indicator_num == 3

zero_line_plot = plot(0, color=color.gray, editable=false)

//####################################################### True Momentum Indicator #####################################################
data(_open, _close, _length) => 
    s = 0
    for i = 0 to _length
        s := s + (_close > _open[i] ? 1 : _close < _open[i] ? - 1 : 0)
    s

tmo_ob = plot(tmo_plot ? round(14 * .8) : na, color=color.gray, editable=false)
tmo_upper = plot(tmo_plot ? 14 : na, color=color.red, editable=false)
//tmo_zero = plot(tmo_plot ? 0 : na, color=color.gray, editable=false)
tmo_os = plot(tmo_plot ? -round(14 * .8) : na, color=color.gray, editable=false)
tmo_lower = plot(tmo_plot ? -14 : na, color=color.green, editable=false)
fill(tmo_ob, tmo_upper, color=color.red, editable=false)
fill(tmo_os, tmo_lower, color=color.green, editable=false)

//tmo plotting
tmo_ma = ema(data(open, close, 7), 5)
tmo_main = ema(tmo_ma, 3)
tmo_signal = ema(tmo_main, 3)

tmo_mainPlot = plot(tmo_plot ? tmo_main : na, color = tmo_main > tmo_signal ? color.green : color.red, linewidth=2, transp=70, editable=false)
tmo_signalPlot = plot(tmo_plot ? tmo_signal : na, color=tmo_main > tmo_signal ? color.green : color.red, transp=70, editable=false)
plot(tmo_plot and cross(tmo_main, tmo_signal) ? tmo_main : na, color=tmo_main > tmo_signal ? color.green : color.red, style=plot.style_circles, linewidth=3, transp=70, editable=false)
fill(tmo_mainPlot, tmo_signalPlot, color = tmo_main > tmo_signal ? color.green : color.red, transp=70, editable=false)

// tmo higher time frame plot
tmo_plot_custom_tf = input(true, title="True Momentum Indicator - Plot Another Timeframe?")
tmo_resCustom = input(title="Custom Timeframe ", type=input.resolution, defval="W")
tmo_custom_tf = tmo_plot_custom_tf ? tmo_resCustom : timeframe.period

tmo_ma_htf = security(syminfo.tickerid, tmo_custom_tf, tmo_ma,barmerge.gaps_off, barmerge.lookahead_on)
tmo_main_htf = security(syminfo.tickerid, tmo_custom_tf, tmo_main,barmerge.gaps_off, barmerge.lookahead_on)
tmo_signal_htf = security(syminfo.tickerid, tmo_custom_tf, tmo_signal,barmerge.gaps_off, barmerge.lookahead_on)

tmo_main_htf_plot = plot(tmo_plot and tmo_plot_custom_tf ? tmo_main_htf : na, color = tmo_main_htf > tmo_signal_htf ? color.blue : color.yellow, linewidth=2, editable=false)
tmo_signal_htf_plot = plot(tmo_plot and tmo_plot_custom_tf ? tmo_signal_htf : na, color=tmo_main_htf > tmo_signal_htf ? color.blue : color.yellow, editable=false)
plot(tmo_plot and tmo_plot_custom_tf and cross(tmo_main_htf, tmo_signal_htf) ? tmo_main_htf : na, color=tmo_main_htf > tmo_signal_htf ? color.blue : color.yellow, style=plot.style_circles, linewidth=3, editable=false)
fill(tmo_main_htf_plot, tmo_signal_htf_plot, color = tmo_main_htf > tmo_signal_htf ? color.blue : color.yellow , editable=false)

//####################################################### Squeeze Momentum #####################################################
//Bollinger Bands Basis Line
msqz_basis = sma(close, 20)
//Keltner Channel Basis Line
msqz_basiskc = sma(close, 20)
// Keltner Channel Low
msqz_rangemal = sma(tr, 20)
// Keltner Channel Mid
msqz_rangemam = sma(tr, 20)
//Keltner Channel High
msqz_rangemah = sma(tr, 20)
//Bollinger Bands Multiplier
msqz_mult = 2.0
//Keltner Channel Low Multiplier
msqz_multlowKC = 2.0
//Keltner Channel Mid Multiplier
msqz_multmidKC = 1.5
//Keltner Channel High Multiplier
msqz_multhighKC = 1.0

//Bollinger Bands 
msqz_dev = msqz_mult * stdev(close, 20)
msqz_upperBB = msqz_basis + msqz_dev
msqz_lowerBB = msqz_basis - msqz_dev

//Keltner Channel Bands Low 
msqz_upperKCl = msqz_basiskc + msqz_rangemal * msqz_multlowKC
msqz_lowerKCl = msqz_basiskc - msqz_rangemal * msqz_multlowKC

//Keltner Channel Bands Mid
msqz_upperKCm = msqz_basiskc + msqz_rangemam * msqz_multmidKC
msqz_lowerKCm = msqz_basiskc - msqz_rangemam * msqz_multmidKC

//Keltner Channel Bands High
msqz_upperKCh = msqz_basiskc + msqz_rangemah * msqz_multhighKC
msqz_lowerKCh = msqz_basiskc - msqz_rangemah * msqz_multhighKC

//------------------------{ Squeeze Basics }------------------------------------

//Squeeze Momentum
msqz_sz = linreg(close - avg(avg(highest(high, 20), lowest(low, 20)), ema(close, 20)), 20, 0)

//Squeeze Conditions
msqz_sc1 = msqz_sz >= 0
msqz_sc2 = msqz_sz < 0
msqz_sc3 = msqz_sz >= msqz_sz[1]
msqz_sc4 = msqz_sz < msqz_sz[1]
msqz_sc5 = msqz_sz < 1
msqz_sc6 = msqz_sz > -1

//Squeeze On
msqz_lowsqz = msqz_lowerBB > msqz_lowerKCl and msqz_upperBB < msqz_upperKCl
msqz_lsf = msqz_lowsqz == false
msqz_midsqz = msqz_lowerBB > msqz_lowerKCm and msqz_upperBB < msqz_upperKCm
msqz_msf = msqz_midsqz == false
msqz_highsqz = msqz_lowerBB > msqz_lowerKCh and msqz_upperBB < msqz_upperKCh
msqz_hsf = msqz_highsqz == false

//Squeeze Dot Plot Zero Line
//Squeeze Dot Colors
msqz_line_num = 0
msqz_sqzproc = msqz_highsqz ? color.yellow : msqz_midsqz ? #ff1100 : msqz_lowsqz ? color.white : msqz_lowsqz ? #000000 : na
msqz_on_off = msqz_highsqz ? "On | High" : msqz_midsqz ? "On" : "Off"
msqz_sqzproz = msqz_highsqz ? msqz_line_num : msqz_midsqz ? msqz_line_num : msqz_lowsqz ? msqz_line_num : na
plotshape(msqz_plot or moxie_plot ? msqz_sqzproz : na, color=msqz_sqzproc, style=shape.diamond, location=location.absolute, title="TTM Squeeze")


//Squeeze Plot
msqz_color = msqz_sc1 and msqz_sc3 ? color.lime : msqz_sc1 and msqz_sc4 ? color.gray : msqz_sc2 and msqz_sc4 ? color.red : msqz_sc2 and msqz_sc3 ? color.gray : na
plot(msqz_plot ? msqz_sz : na, color=msqz_color, style=plot.style_columns, linewidth=1, editable=false, transp=70)

//Start Of Squeeze
//sol         = barssince(lsf) == 1
//som         = barssince(msf) == 1
//soh         = barssince(hsf) == 1

//End Of Squeeze
//eol         = barssince(lowsqz) == 1
//eom         = barssince(midsqz) == 1
//eoh         = barssince(highsqz) == 1

msqz_info = msqz_sc1 and msqz_sc3 or msqz_sc1 and msqz_sc4 ? "SQZ | Bullish | " + msqz_on_off : msqz_sc2 and msqz_sc4 or msqz_sc2 and msqz_sc3 ? "SQZ | Bearish | " + msqz_on_off : "SQZ | Neutral | " + msqz_on_off

//###################### Moxie Arrows #################
funcMoxie() =>
    vc1 = ema(close , 12) - ema(close, 26)
    va1 = ema(vc1, 9)
    sData = (vc1 - va1) * 3

s2 = timeframe.period == '1' or timeframe.period == '3' ? security(syminfo.tickerid, '5', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == '5' ? security(syminfo.tickerid, '15', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == '15' ? security(syminfo.tickerid, '30', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == '30' ? security(syminfo.tickerid, '60', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == '45' ? security(syminfo.tickerid, '120', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == '60' or timeframe.period == '120' or timeframe.period == '180' or timeframe.period == '240' ? security(syminfo.tickerid, 'D', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == 'D' ? security(syminfo.tickerid, 'W', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : timeframe.period == 'W' ? security(syminfo.tickerid, 'M', funcMoxie(), barmerge.gaps_off, barmerge.lookahead_on)
  : na

//zero_line_plot = plot(0, color=color.gray, editable=false)

s2_color = s2 >= 0 ? s2 > s2[1] ? color.fuchsia : color.purple :  s2 < s2[1] ? color.orange : color.yellow
s2_plot = plot(moxie_plot ? s2 : na, color=s2_color, editable=false)
fill(zero_line_plot, s2_plot, color= moxie_plot ? (s2 < 0 ? color.red : color.green) : na)

buy_setup = int(na)
sell_setup = int(na)

setup = sign(s2 - s2[1]) > sign(s2[1] - s2[2]) ? 1 : sign(s2 - s2[1]) < sign(s2[1] - s2[2]) ? 2 : -1
setup_color = setup == 1 ? color.green : setup == 2 ? color.red : color.orange

if (setup == 1) //green
    sell_setup := 0
    buy_setup := nz(buy_setup[1]) + 1
else
    if (setup == 2) //red
        buy_setup := 0
        sell_setup := nz(sell_setup[1]) + 1
    else
        if (setup == -1) //orange
            buy_setup := nz(buy_setup[1])
            sell_setup := nz(sell_setup[1])


plotshape(moxie_plot and buy_setup==2 ? s2 : na, style=shape.arrowup, color=color.green, location = location.absolute, size=size.normal, editable=false)
plotshape(moxie_plot and sell_setup==2 ? s2 : na, style=shape.arrowdown, color=color.red, location = location.absolute, size=size.normal, editable=false)
bgcolor(color = moxie_plot and buy_setup==2 ? color.green : na, transp=70, editable=false)
bgcolor(color = moxie_plot and sell_setup==2 ? color.red : na, transp=70, editable=false)