//@version=4
study(title = "Accumulation/Distribution", shorttitle="ACD")

o = open
c = close
h = high
l = low

totalall(s) => 
    totalall = float(na)
    totalall := na(totalall[1]) ? s : totalall[1] + s
    totalall

highestall(s) => 
    maxall = float(na)
    maxall := na(maxall[1]) ? s : max(maxall[1], s)
    maxall

lowestall(s) => 
    minall = float(na)
    minall := na(minall[1]) ? s : min(minall[1], s)
    minall
    
trueRange() => na(h[1]) ? h-l : max(max(h - l, abs(h - c[1])), abs(l - c[1]))

f_ma(type, src, len) =>
    float result = 0
    if type == "EMA"
        result := ema(src, len)
    if type == "SMA"
        result := sma(src, len)
    if type == "RMA"
        result := rma(src, len)
    result

//###########True Momentum Indicator//////////
tmi_s = 0
for i = 0 to 14
    tmi_s := tmi_s + (c > o[i] ? 1 : c < o[i] ? - 1 : 0)
tmi_data = tmi_s
    
tmi_MA = f_ma("EMA", tmi_data, 5)
tmi_Main = f_ma("EMA", tmi_MA, 3)
tmi_Signal = f_ma("EMA", tmi_Main, 3)

//fish
fish_price = hl2
fish_length = 10
fish_maxHigh = highest(fish_price, fish_length)
fish_minLow = lowest(fish_price, fish_length)
fish_range = fish_maxHigh - fish_minLow
fish_sto = fish_range != 0 ? (fish_price - fish_minLow) / fish_range : 0

fish_value =float(na)
fish_value := (0.66 * (fish_sto - 0.5)) + (0.67 * nz(fish_value[1]))
fish_trunc_value = fish_value > 0.99 ? 0.999 : fish_value < -0.99 ? -0.999 : fish_value
fish = 0.0
fish := (0.5 * log((1 + fish_trunc_value) / (1 - fish_trunc_value))) + (0.5 * nz(fish[1]))
fish_sig = fish > nz(fish[1]) ? 1 : fish < nz(fish[1]) ? -1 : 0

fish_buy_cond = crossover(fish_sig, 0) and tmi_Main > tmi_Signal
fish_sell_cond = crossunder(fish_sig, 0) and tmi_Main <= tmi_Signal

plotshape(fish_buy_cond, color=color.green, style=shape.arrowup, location=location.bottom,  editable=false)
plotshape(fish_sell_cond, color=color.red, style=shape.arrowdown, location=location.bottom,  editable=false)

//###########Accumlation Distrbution//////////
acd_data = c > c[1] ? c - min(c[1], l) : c < c[1] ? c - max(c[1], h) : 0
acd_ad = totalall(acd_data)
acd_accDist = (acd_ad - lowestall(acd_ad)) /(highestall(acd_ad) - lowestall(acd_ad))

acd_smoothK = 14
acd_smoothD = 1
acd_k = sma(stoch(c, h, l, acd_smoothK), 3)
acd_d = sma(acd_k, acd_smoothD)
acd_slowd =  (acd_d - lowestall(acd_d)) /(highestall(acd_d) - lowestall(acd_d))

acd_p1=hline(1, color = color.red, editable=false)
hline(0.886, editable=false)
acd_p2=hline(0.786, color = color.red, editable=false)
fill(acd_p1,acd_p2, color = color.red, editable = false, transp = 90)
acd_p3 = hline(0.618, color = color.gray, editable=false)
acd_p4 = hline(0.5, color = color.gray, editable=false)
fill(acd_p3,acd_p4, color = color.silver, editable = false, transp = 90)
hline(0.382, editable=false)
hline(0.236, editable=false)
acd_p5 = hline(0.146, color = color.lime, editable=false)
acd_p6 = hline(0, color = color.lime, editable=false)
fill(acd_p5,acd_p6, color = color.lime, editable = false, transp = 90) 

acd_color = color.white

plot(acd_accDist, color = acd_color, editable=false, linewidth=2)
plot(acd_slowd, color=color.aqua, editable=false, display=display.none)

//################## Bullish Bearsh Neutral Chop Strength Labels ##############
bbc_AvgLength = 13
bbc_Lengthchop = 13
bbc_Choppy = 61.8
bbc_MidLine = 50
bbc_Trending = 38.2
bbc_AVG = hma(c, bbc_AvgLength)

bbc_CI = ((log(sum(trueRange(), bbc_Lengthchop) / (highest(h >= c[1] ? h : c[1], bbc_Lengthchop) - lowest(l <= c[1] ? l : c[1], bbc_Lengthchop))) / log(10)) / (log(bbc_Lengthchop) / log(10))) * 100

bbc_chop_level = bbc_CI > bbc_MidLine and bbc_CI < 62  ? "MILD CHOP " + tostring(round(bbc_CI)) : bbc_CI >= 61.8 ? "STRONG CHOP " + tostring(round(bbc_CI)) : bbc_CI <= bbc_MidLine and bbc_CI > 31.8 ? "MILD TRENDING " + tostring(round(bbc_CI)) : "STRONG TRENDING " + tostring(round(bbc_CI))
bbc_chop_label_color = bbc_CI > bbc_MidLine ? color.yellow : color.fuchsia

bbc_trend_score = int(na)
for _l = 11 to 20
    bbc_trend_score := nz(bbc_trend_score) + (c >= c[_l] ? 1 : -1)
        
bbc_col1 = bbc_trend_score > 5
bbc_col2 = bbc_trend_score < -5
bbc_col_neutral = not bbc_col1 and not bbc_col2

bbc_bull_bear_label = bbc_col1 ? "Bullish" : bbc_col2 ? "Bearish" : bbc_col_neutral ? "Neutral" : na
bbc_bull_bear_label_color = bbc_col1 ? color.green : bbc_col2 ? color.red : bbc_col_neutral ? color.yellow : na

var label _chop_label = na
label.delete(_chop_label)
_chop_label := label.new(x=bar_index, y=acd_accDist - 0.1 , text=bbc_chop_level, color=bbc_chop_label_color, style=label.style_label_up)

var label _bull_bear_label = na
label.delete(_bull_bear_label)
_bull_bear_label := label.new(x=bar_index, y=acd_accDist + 0.1 , text=bbc_bull_bear_label, color=bbc_bull_bear_label_color)