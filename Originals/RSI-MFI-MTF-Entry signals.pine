//@version=3
//This indicator is designed to show give early entry signals as well as to follow trend moves, according to different settings,
//but can be used for various other strategies or confirmation signals.
//
//The indicator shows a histogram of the RSI/MFI or STOCH in relation to an ema of the RSI/MFI or STOCH.
//The histogram is then smoothed to give early reversal/entry signals.
//The actual RSI/MFI/STOCH line with oversold/overbought indication can be displayed or omitted, as preferred.
//in addition to the RSI/MFI/STOCH line or as an alternative to it, the background colour can be set to change folowing the RSI/MFI signals.
//The timeframe can be chosen. Higher timeframes (eg. 3h/1D) tend to give less false signals.
//
//features. 
//support for custom Multiple Time Frame selection.
//option for choice of RSI/MFI or STOCH as base indicator.
//option for price bar coloring according to the indicator. (deselecting "borders" in the "style" tab is recommended)
//THE INDICATOR CAN NOW AUTOMATICALLY DRAW TRENDLINES on the histogram and give alerts if the trendline is broken!
//FOR CLEANER CHARTS the indicator can be altered to show only the last extended trendlines
//this can be done by changing the number in line 175 "_show_last=10000" (higher numbers give more lines)
//
//price bar coloring can be adjusted for different strategies:
//1. following the slope of the histogram (for faster entry/exit signals)
//2. according to positive or negative histogram (for longer moves)
//3. according to pos. or neg. RSI/MFI (for longer term trend holds)
//4. uptrend: biased towards faster buy signals and slower sell signals to stay in the uptrend
//5. downtrend: biased towards faster sell signals and slower buy signals to stay in the downtrend
//6. "auto trend": trend(strenghth) is determined based on ADX
//7. "trendline break": looking for upwards or downwards breaks of the trendline of the highs and lows, lookback period low and high should be adjusted for the specifiek secutrity!!
//
//A longer timeframe (eg. 3x) is recommended for following trend moves.
//try different strategies and values to see what works better for RSI/MFI/STOCH and for different markets.
//
//version 7.
//* resolved issue with the indicator repainting while on a longer timeframe until that longer timeframe has ended.
//  An option is now added to calculate the longer timeframe based on the current price action.
//  this gives a little more noise but doesn't repaint anymore!
//* added automatic trendlines that connect the highs and the lows on the histogram, this can be used for several purposes:
//1. indication of RSI-divergeance which may lead to a reversal in the near future.
//2. if the histogram breaks the higher or lower trendline, this usually results in a reversal to the upper or lower side
//3. if you find other uses,... please let me know!! ;-)
//
//JD.

study(title="RSI/MFI/Stoch - MTF - Entry signals/Trend colored bars - JD", shorttitle="RSI/MFI/Stoch - MTF - Entry signals/Trend colored bars - JD")
useCurrentRes = input(false, title="Use Current Chart Resolution?")
resCustom = input(title="Use Different Timeframe? Uncheck Box Above", type=resolution, defval="D")
useCurrentprice = input(true, title="Use Current Resolution price action for more detail and NO REPAINT! IF UNCHECKED, COLORS CAN/WILL REPAINT (!) and are only valid and final once the custom timeframe has closed!!!")
show_mfi=input(1, minval = 1, maxval = 3, title="base indicator: 1= RSI, 2= MFI, 3= stoch")
src = close, base_len = input(14, minval=1, title="RSI/MFI/Stoch Length"), len2=input(23, minval=1, title="Smoothing Length")
th1=input(70, minval=1, title="overbought")
th2=input(30, minval=1, title="oversold")
show_rsi=input(true, title="show RSI/MFI/Stoch line")
show_bg=input(false, title="show RSI/MFI/Stoch background color")
color_bars=input(true, title="color pricebars (deselecting 'borders' in the 'style' tab is recommended)")
color_base=input(7, minval = 1, maxval = 7, title="color bars based on: 1= histogram slope, 2=+/- histogram, 3=+/- rsi/mfi/stoch-line, 4= uptrend (buy bias), 5= downtrend (sell bias), 6= auto trend detection (ADX based), 7= trendline break (BETA)")
show_lines=input(true,title="show highs/lows trendlines (dotted lines are plotted in realtime, full lines with delay = lookback length")
show_cross=input(false,title="show trendline break crosses")

tllen_l=input(20,title="trendline lookback length - lows")
tllen_h=input(20,title="trendline lookback length - highs")

res = useCurrentRes ? period : resCustom

//timeframe conversion
base_res=period=="1"?1:period=="3"?3:period=="5"?5:period=="15"?15:period=="30"?30:period=="45"?45:period=="60"?60:period=="120"?120:period=="180"?180:period=="240"?240:period=="D"?1440:period=="W"?10080:period=="M"?302400:na
custom_res=resCustom=="1"?1:resCustom=="3"?3:resCustom=="5"?5:resCustom=="15"?15:resCustom=="30"?30:resCustom=="45"?45:resCustom=="60"?60:resCustom=="120"?120:resCustom=="180"?180:resCustom=="D"?1440:resCustom=="W"?10080:na
res_factor=useCurrentRes?1:base_res>custom_res?1:round(custom_res/base_res)
len= useCurrentRes? base_len : useCurrentprice? base_len*res_factor:base_len

//MFI calcutation
TypPrice = hlc3
upper_s = sum(volume * (change(TypPrice) <= 0 ? 0 : TypPrice), len)
lower_s = sum(volume * (change(TypPrice) >= 0 ? 0 : TypPrice), len)
mf = rsi(upper_s, lower_s)

//stoch calculation
st = stoch(close,high,low,len)

//RSI/MFI/Stoch selection
rsi_base = show_mfi==1?rsi(src,len):show_mfi==2?mf:st

//RSI output MTF
rsi = security(tickerid, useCurrentprice?period:res, rsi_base)
rsi_MTF = security(tickerid, res, rsi_base)
smooth_base=ema(rsi_base,len)
smooth = useCurrentprice?ema(rsi,len):security(tickerid, res, smooth_base)

Diff=rsi-smooth

//alternative version MA definition
//smooth2=ema(ema(Diff,res_factor),len2)

//2nd version MA TEMA definition
sema1 = ema(Diff,res_factor)
sema2 = ema(sema1, len2)
sema3 = ema(sema2, len2)
smooth2= 3 * (sema1 - sema2) + sema3

smooth_up=smooth>=smooth[1]?true:false
smooth2_up=smooth2>=smooth2[2]?true:false

//ADX and DI trend calculation
TrueRange = max(max(high-low, abs(high-nz(close[1]))), abs(low-nz(close[1])))
DirectionalMovementPlus = high-nz(high[1]) > nz(low[1])-low ? max(high-nz(high[1]), 0): 0
DirectionalMovementMinus = nz(low[1])-low > high-nz(high[1]) ? max(nz(low[1])-low, 0): 0

calc_DIPlus()=>
    base_DIPlus = na
    SmoothedTrueRange=na
    SmoothedTrueRange := nz(SmoothedTrueRange[1]) - (nz(SmoothedTrueRange[1])/len) + TrueRange
    SmoothedDirectionalMovementPlus=na
    SmoothedDirectionalMovementPlus := nz(SmoothedDirectionalMovementPlus[1]) - (nz(SmoothedDirectionalMovementPlus[1])/len) + DirectionalMovementPlus
    base_DIPlus := SmoothedDirectionalMovementPlus / SmoothedTrueRange * 100
calc_DIMinus()=>
    base_DIMinus = na
    SmoothedTrueRange=na
    SmoothedTrueRange := nz(SmoothedTrueRange[1]) - (nz(SmoothedTrueRange[1])/len) + TrueRange
    SmoothedDirectionalMovementMinus=na
    SmoothedDirectionalMovementMinus := nz(SmoothedDirectionalMovementMinus[1]) - (nz(SmoothedDirectionalMovementMinus[1])/len) + DirectionalMovementMinus
    base_DIMinus := SmoothedDirectionalMovementMinus / SmoothedTrueRange * 100

//ADX output MTF
HTF=base_res>custom_res?true:false
DIPlus = security(tickerid, useCurrentprice?period:res, calc_DIPlus())
DIPlusS = security(tickerid, res, calc_DIPlus())
DIMinus = security(tickerid, useCurrentprice?period:res, calc_DIMinus())
DIMinusS = security(tickerid, res, calc_DIMinus())

DIdiff = HTF?(DIPlus - DIMinus):(DIPlusS - DIMinusS)
DIcolor = DIdiff<0?red:DIdiff>0?green:yellow
DX = HTF?(abs(DIPlus-DIMinus) / (DIPlus+DIMinus)*100):(abs(DIPlusS-DIMinusS) / (DIPlusS+DIMinusS)*100)
ADX = sma(DX, len)

momentprod=useCurrentprice and HTF?DIdiff*ADX*res_factor/2:DIdiff*ADX
smooth_ADX=ema(momentprod,len) 
strong_trend=((DIdiff<0 and smooth_ADX<=smooth_ADX[1]) or (DIdiff>0 and smooth_ADX>=smooth_ADX[1]))?true:false

//color definition
emacolor=Diff>=0?(smooth2_up?color(green,0):color(green,50)):(smooth2_up?maroon:red)
smooth2color=smooth2>=smooth2[2]?green:red
bcol= rsi_MTF<th2?color(red,75):rsi_MTF>th1?color(lime,85):rsi>50?color(green,90):color(maroon,85)
rsicol=bcol==color(red,75)?red:bcol==color(maroon,85)?color(maroon,20):bcol==color(lime,85)?lime:bcol==color(green,90)?color(green,30):na
plot(Diff, title="RSI - histo", style=histogram, color=emacolor,linewidth=2,transp=0)
plot(smooth2,title="RSI - ma", color=smooth2color,linewidth=3,transp=20)
plot(show_rsi?rsi-50:na,linewidth=2,color=rsicol,transp=0)
bgcolor(title="RSI overbought/oversold",color=show_bg?bcol:na)

//trendline pivots
last_high1=pivothigh(Diff,tllen_h,tllen_h)
last_low1=pivotlow(Diff,tllen_l,tllen_l)
color_high=highest(Diff,2*tllen_h)<highest(Diff,5*tllen_h)?fuchsia:na
color_low=lowest(Diff,2*tllen_l)>lowest(Diff,5*tllen_l)?lime:na
_barshigh=barssince(Diff >= last_high1)
_barslow=barssince(Diff <= last_low1)

//last high-low point
lh1=highest(Diff,tllen_h)
lh2=highest(Diff,6*tllen_h)
lh3=highest(Diff,10*tllen_h)
//indication of possible reversal or H/S pattern
high_falling=lh1<lh2 and lh2<lh3?true:false

last_high1_period=barssince(Diff >= lh1)
last_high2_period=barssince(Diff >= lh2)

ll1=lowest(Diff,tllen_l)
ll2=lowest(Diff,5*tllen_l)
ll3=lowest(Diff,12*tllen_l)
//indication of possible reversal or inv. H/S pattern
low_rising=ll1>ll2 and ll2>ll3?true:false

//trendline plot
plot(show_lines?last_high1:na,title="trendline high",color=color_high,offset=-1*tllen_h,transp=0)
plot(show_lines?last_low1:na,title="trendline low",color=color_low,offset=-1*tllen_l,transp=0)

//ext high trendlines
_show_last=10000
Ph1y=fixnan(last_high1)
Ph2y=na
Ph2y:=change(Ph1y)!=0?Ph1y[1]:Ph2y[1]
Ph1x = 0
Ph2x=na
Ph2x := change(Ph1y)!=0?barssince(change(Ph1y[1])!=0):Ph2x[1]
Ph2xbis=barssince(change(Ph1y)!=0)
// point slope
mh=na
mh :=change(Ph1y)!=0? ((Ph1y-Ph2y) / (Ph2x)):mh[1]
// plot range
ext_high_tline = Ph1y + mh*(Ph2xbis)
ext_high_tline_RT = Ph1y + mh*(Ph2xbis+tllen_h)
color_ext_tl_high=mh<=0?fuchsia:na
plot(show_lines?mh<=0 and ext_high_tline_RT[tllen_h]!=ext_high_tline?ext_high_tline:na:na, title="ext trendline high", color=color_ext_tl_high, linewidth=1, style=linebr, transp=40,offset=-tllen_h,show_last=_show_last)
plot(show_lines?mh<=0?ext_high_tline_RT:na:na, title="ext trendline high RT", color=color_ext_tl_high, linewidth=1, style=circles, transp=40,show_last=_show_last)

//ext low trendlines
Pl1y=fixnan(last_low1)
Pl2y=na
Pl2y:=change(Pl1y)!=0?Pl1y[1]:Pl2y[1]
Pl1x = 0
Pl2x=na
Pl2x := change(Pl1y)!=0?barssince(change(Pl1y[1])!=0):Pl2x[1]
Pl2xbis=barssince(change(Pl1y)!=0)
// point slope
ml=na
ml :=change(Pl1y)!=0? ((Pl1y-Pl2y) / (Pl2x)):ml[1]
// plot range
ext_low_tline = Pl1y + ml*(Pl2xbis)
ext_low_tline_RT = Pl1y + ml*(Pl2xbis+tllen_l)
color_ext_tl_low=ml>=0?lime:na
plot(show_lines?ml>=0 and ext_low_tline_RT[tllen_l]!=ext_low_tline?ext_low_tline:na:na, title="ext trendline low", color=color_ext_tl_low, linewidth=1, style=linebr, transp=40,offset=-tllen_l,show_last=_show_last)
plot(show_lines?ml>=0?ext_low_tline_RT:na:na, title="ext trendline low RT", color=color_ext_tl_low, linewidth=1, style=circles, transp=40,show_last=_show_last)

//detect trendline breaks
avg_Diff=ema(Diff,res_factor)
break_tl_high=mh<=0?crossover(avg_Diff,ext_high_tline_RT):false
break_tl_low=ml>=0?crossunder(avg_Diff,ext_low_tline_RT):false
cross_color=break_tl_high?lime:break_tl_low?fuchsia:yellow
above_tl_high=avg_Diff>=ext_high_tline?true:false
below_tl_low=avg_Diff<=ext_low_tline?true:false

//mark trendline crosses
YPosition = break_tl_high?ext_high_tline_RT:break_tl_low?ext_low_tline_RT:na
plotshape(show_lines and show_cross and break_tl_high? YPosition : na,style=shape.triangleup, location=location.absolute,size=size.tiny, color=cross_color,transp=0)
plotshape(show_lines and show_cross and break_tl_low? YPosition : na,style=shape.triangledown, location=location.absolute,size=size.tiny, color=cross_color,transp=0)

//final coloring
trend_color=strong_trend?DIcolor:(smooth2>=0 or ema(Diff,res_factor)>=0?(high_falling?red:green):(low_rising?green:red))
trendline_color=Diff<ext_low_tline_RT?Diff>0?smooth2color:red:Diff>ext_high_tline_RT?Diff<0?smooth2color:green:(ema(Diff,res_factor)>=0?green:red)
barcolor_ref=color_base==1?smooth2color:color_base==2?(ema(Diff,res_factor)>=0?green:red):color_base==3?((useCurrentprice?ema(rsi,res_factor):rsi)>50?green:red):color_base==4?(smooth2>=0 or ema(Diff,res_factor)>=0?green:(low_rising?smooth2color:red)):color_base==5?(smooth2<=0 or ema(Diff,res_factor)<=0?red:(high_falling?smooth2color:green)):color_base==6?trend_color:color_base==7?trendline_color:na
barc=barcolor_ref==green?color(lime,30):barcolor_ref==red?color(red,20):yellow
barcolor(color_bars?barc:na)
baseline_col=strong_trend?DIcolor:na
plot(0,color=baseline_col,transp=0,linewidth=2)

//bar coloring
colorchange= barc==barc[1]?false:true

//option for long and short signals or just "trend change" signal you can use if you only have a basic account with one alert max.

//possible long/short-conditions
longcondition=colorchange and barc==color(lime,30)
shortcondition=colorchange and barc==color(red,20)
longcondition_tl_high=(Diff>ext_high_tline and Diff[1]<=ext_high_tline)?true:false
shortcondition_tl_low=(Diff<ext_low_tline and Diff[1]>=ext_low_tline)?true:false
alertcondition(longcondition,"long","Long")
alertcondition(shortcondition,"short","Short")
alertcondition(colorchange,"trend change","Trend Change")
alertcondition(longcondition_tl_high or shortcondition_tl_low,"trendline break","Trendline Break")